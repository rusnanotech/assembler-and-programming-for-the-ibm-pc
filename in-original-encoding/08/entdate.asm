		page 60,132
TITLE	ENTDATE (COM) ; Упр. 8.5. Ввод с клавиатуры расширенной DOS
CODESG	SEGMENT	PARA 'Code'
		ASSUME	CS:CODESG,DS:CODESG,SS:CODESG
		ORG		100H
BEGIN:	JMP		MAIN
;--------------------------------------------------------------
;		Данные с версией программы и копирайтом:
TAB		EQU		09
CR		EQU		13			; Символ возврата каретки 0DH
LF		EQU		10			; Символ перевода (конца) строки 0AH
VERSION	DB		'EntDate Version 1.0',CR,LF,'Copyright (C) V.A. Markov 2017. All rights reserved',CR,LF

;		Данные с сообщениями запросов:
DDAY	DB		CR,LF,'Enter day ("dd", between 01 and 31): ',CR,LF
DMON	DB		CR,LF,'Enter month ("mm", between 01 and 12): ',CR,LF
DYEA	DB		CR,LF,'Enter year ("yyyy"): ',CR,LF
DERR	DB		CR,LF,TAB,'^ Error'
MONTHS	DB		'Jan.','Feb.','Mar.','Apr.','May ','June','July','Aug.','Sept','Oct.','Nov.','Dec.'

;		Область ввода и преобразования даты
DATE	DB		CR,LF,'Date: '
DAY		DB		3 DUP(' ')			; Поля для дня, месяца, года
MON		DB		5 DUP(' ')	
YEA		DB		4 DUP(' ')
MAXDR	DB		'39', 31			; Макс. разряды и сумма
MAXMR	DB		'19', 12
MAXYR	DB		'999', 2 DUP (00H)
NN		DB		?					; Результат для выбора месяца
;--------------------------------------------------------------
;		Главная процедура:
MAIN	PROC	NEAR
		CALL	DISPVER			; Вывод версии программы
		CALL	ENTDAY			; Приглашение ввода ДНЯ
		JZ		EXIT			; Выход, если был возврат каретки
		CALL	ENTMON			; Приглашение ввода месяца
		JZ		EXIT			; Выход, если был возврат каретки
		CALL	ENTYEA			; Приглашение ввода года
		JZ		EXIT			; Выход, если был возврат каретки
		CALL	DISPDAT
EXIT:	RET
MAIN	ENDP
;--------------------------------------------------------------
;		Вывод версии программы:
DISPVER	PROC
		LEA		DX,VERSION	; Адрес сообщения о версии программы
		MOV		CX,74		; Макс. длина сообщения (в байтах)
		CALL	DISP		; Вызов процедуры вывода
		RET
DISPVER	ENDP
;--------------------------------------------------------------
;		Ввод дня:
ENTDAY	PROC
D10:	CALL	DISPDAY		; Введите день
		CALL	KEYBDAY		; Ввод дня
		JZ		DEXIT		; Выход, если был возврат каретки
		LEA		SI,MAXDR
		MOV		CX,01		; Кол-во циклов проверок
		CALL	CTRL		; Проверка введенных данных
		LEA		CX,DERR		; Была ли ошибка?
		SUB		CX,DX
		JZ		D10			; Повторить ввод, если ошибка
DEXIT:	RET
ENTDAY	ENDP
;--------------------------------------------------------------
;		Введите день:
DISPDAY	PROC
		LEA		DX,DDAY		; Адрес запроса ввода дня
		MOV		CX,41		; Макс. длина данных (в байтах)
		CALL	DISP		; Вызов процедуры вывода
		RET
DISPDAY	ENDP
;--------------------------------------------------------------
;		Ввод дня:
KEYBDAY	PROC
		LEA		DX,DAY		; Адрес области для записи дня
		MOV		CX,5		; Макс. длина данных (в байтах) НОВ
		CALL	KEYB		; Вызов процедуры ввода
		RET
KEYBDAY	ENDP
;--------------------------------------------------------------
;		Ввод месяца:
ENTMON	PROC
M10:	CALL	DISPMON		; Введите месяц
		CALL	KEYBMON		; Ввод месяца
		JZ		MEXIT		; Выход, если был возврат каретки
		LEA		SI,MAXMR
		MOV		CX,01		; Кол-во циклов проверок
		CALL	CTRL		; Проверка введенных данных
		LEA		CX,DERR		; Была ли ошибка?
		SUB		CX,DX
		JZ		M10			; Повторить ввод, если ошибка
MEXIT:	RET
ENTMON	ENDP
;--------------------------------------------------------------
;		Введите день:
DISPMON	PROC
		LEA		DX,DMON		; Адрес запроса ввода месяца
		MOV		CX,43		; Макс. длина данных (в байтах)
		CALL	DISP		; Вызов процедуры вывода
		RET
DISPMON	ENDP
;--------------------------------------------------------------
;		Ввод дня:
KEYBMON	PROC
		LEA		DX,MON		; Адрес области для записи месяца
		MOV		CX,4		; Макс. длина данных (в байтах)
		CALL	KEYB		; Вызов процедуры ввода
		RET
KEYBMON	ENDP
;--------------------------------------------------------------
;		Ввод года:
ENTYEA	PROC
Y10:	CALL	DISPYEA		; Введите год
		CALL	KEYBYEA		; Ввод года
		JZ		YEXIT		; Выход, если был возврат каретки
		LEA		SI,MAXYR
		MOV		BX,03
		MOV		CX,02		; Кол-во циклов проверок
		CALL	CTRL		; Проверка введенных данных
		LEA		CX,DERR		; Была ли ошибка?
		SUB		CX,DX
		JZ		Y10			; Повторить ввод, если ошибка
YEXIT:	RET
ENTYEA	ENDP
;--------------------------------------------------------------
;		Введите день:
DISPYEA	PROC
		LEA		DX,DYEA		; Адрес запроса ввода года
		MOV		CX,25		; Макс. длина данных (в байтах)
		CALL	DISP		; Вызов процедуры вывода
		RET
DISPYEA	ENDP
;--------------------------------------------------------------
;		Ввод дня:
KEYBYEA	PROC
		LEA		DX,YEA		; Адрес области для записи года
		MOV		CX,7		; Макс. длина данных (в байтах)
		CALL	KEYB		; Вызов процедуры ввода
		RET
KEYBYEA	ENDP
;---------------------------------------------------------------
;		Вывод даты:
DISPDAT	PROC
		MOV		AL,NN
		DEC		AL
		MOV		CX,04		; Множитель и количество циклов
		MUL		CL			; Смещеие относительно MONTHS
		LEA		SI,MONTHS
		ADD		SI,AX		; Адрес названия месяца
		LEA		DI,MON		; Адрес записи названия месяца
		
DAT10:	MOV		AL,[SI]
		MOV		[DI],AL
		INC		SI
		INC		DI
		LOOP	DAT10

		LEA		DX,DAY		; Адрес области данных с сообщением
		MOV		CX,12		; Макс. длина сообщения (в байтах)*
		MOV		DAY+2,20H	; Убрать управл. символ
		CALL	DISP
		
		RET
DISPDAT ENDP
;---------------------------------------------------------------
;		Проверка:
CTRL	PROC
;		Подготовка:
		CMP		AX,05		; НОВ
		JZ		CERR		; НОВ
		CMP		AX,07		; НОВ
		JZ		CERR		; НОВ
		MOV		DI,DX
C10:	MOV		AL,[DI]		; Загрузка старшего разряда
		MOV		DL,[DI+1]	; 	младшего разряда
;		Первичная проверка:
		CMP		BX,03		; Если проверяется год,
		JZ		C20			;  то пропустить след. шаги
		MOV		AH,DL		; Обнаружение '0','0'
		CMP		AX,3030H	;
		JZ		CERR		; Ошибка, если обнаружено
;		Проверка старшего разряда Xx:
C20:	CMP		AL,[SI]		; Выше?
        JA		CERR	    ;  если да, ошибка	
		CMP		AL,30H		; Ниже 30H?
		JB		CERR		;  если да, ошибка
;		Проверка младшего разряда xX:
		INC		SI
		CMP		DL,[SI]		; Выше?
        JA		CERR	    ;  если да, ошибка
		CMP		DL,30H		; Ниже 30H?
		JB		CERR		;  если да, ошибка
		INC		DI
		INC		DI
		LOOP	C10			; Повторить цикл, если год
;		Проверка суммы разрядов:
		CMP		BX,03		; Если год, пропустить проверку суммы
		JZ		CEXIT		;  разрядов.
		INC		SI
		MOV		BL,10		; Множитель
		SUB		AL,30H		; Перевод в число
		SUB		DL,30H
		MUL		BL			; Умножение старшего разряда
		ADD		AL,DL		; Сумма разрядов
		CMP		AL,[SI]		; Проверка
		JA		CERR
		MOV		NN,AL		; Сохранение результата
		
		JMP		CEXIT
CERR:	CALL 	DISPERR
CEXIT:	INC		SI			; Обнуление флага ZF
		RET
CTRL	ENDP
;--------------------------------------------------------------
;		Вывод сообщения об ошибке:
DISPERR	PROC
		LEA		DX,DERR		; Адрес сообщения с ошибкой
		MOV		CX,10		; Макс. длина данных (в байтах)
		CALL 	DISP
		RET
DISPERR	ENDP
;--------------------------------------------------------------
;		Вывод на экран:
DISP	PROC
		MOV		AH,40H		; Запрос вывода на экран
		MOV		BX,01		; Выводное устройство (1 - экран)
		INT		21H			; Вызов DOS
		RET
DISP	ENDP
;--------------------------------------------------------------
;		Ввод данных с клавиатуры:
KEYB	PROC
		MOV		AH,3FH		; Запрос ввода
		MOV		BX,00		; Устройство (0 - клавиатура)
		INT		21H			; Вызов DOS
		CMP		AX,02
		RET
KEYB	ENDP
;--------------------------------------------------------------
CODESG	ENDS
		END		BEGIN

;* Для исключения случаев установки флага ZF в нуль.
;  Данный флаг понадобится для выхода из программы.









